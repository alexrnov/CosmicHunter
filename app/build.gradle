apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "alexrnov.cosmichunter"
        minSdkVersion 14
        // To maintain your application along with each Android release, you should increase the value of this attribute
        // to match the latest API level, then thoroughly test your application on the corresponding platform version.
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        // Determine that AndroidJUnitRunner as the default test instrumentation runner
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        // multiDexEnabled true

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }

    // Gradle automatically adds 'android.test.runner' as a dependency
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    // make your app to be compiled by java8 compiler. Necessary for work Truth
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions { // for Robolectric:
        unitTests.includeAndroidResources = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    flavorDimensions "default"

    /*
    Make a separate flavors for the tests. This is necessary to use the automator
    library with sdk api 18 (android 4.3). To start the flavors with uitests, you need
    go to Menu -> Build -> Select Build Variant -> Active Build Variant = uiTestDebug
    solution kevinpelgrims https://overcoder.net/q/1000830/
     */
    productFlavors {
        production {
            minSdkVersion 14
        }
        uiTest {
            minSdkVersion 18
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    // add maintain Android Design Support Library (begin with Android 2.1 API 7)
    // Starting with Support Library release 26.0.0 (July 2017), the minimum supported
    // API level across most support libraries has increased to Android 4.0 (API level 14) for most library packages.
    // final version of the support library: version 28.0.0, after used androidx
    implementation 'com.google.android.material:material:1.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.12' // required - JUnit4 framework
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // optional - Robolectric environment
    androidTestImplementation 'androidx.test:core:1.2.0'
    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:truth:1.2.0'
    androidTestImplementation 'com.google.truth:truth:0.42'
    // require for Truth library
    implementation "com.google.truth.extensions:truth-java8-extension:0.42"
    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // for interacted with RecyclerViews
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.2.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.2.0'
    // The following Espresso dependency can be either "implementation"
    // or "androidTestImplementation", depending on whether you want the
    // dependency to appear on your APK's compile classpath or the test APK classpath.
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'
    // optional - Mockito framework
    testImplementation "org.mockito:mockito-core:2.23.4"
    // optional - hamcrest library
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    // optional -- UI testing with UI Automator (begin from API 18 Android 4.3)
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestUtil 'androidx.test:orchestrator:1.2.0'

    // for room database
    def room_version = "2.2.3"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version" // For Kotlin use kapt instead of annotationProcessor
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"
    // Cannot fit requested classes, maybe require androidx.room:room-guava:$room_version
    // optional - RxJava support for Room
    //implementation "androidx.room:room-rxjava2:$room_version"
    // error duplicate classes
    // optional - Guava support for Room, including Optional and ListenableFuture
    //implementation "androidx.room:room-guava:$room_version"
    // Test helpers
    testImplementation "androidx.room:room-testing:$room_version"
}

repositories {
    google()
    mavenCentral()
}

// necessary to cover kotlin classes with tests.
// execute gradlew copyTestClasses command in the console
task copyTestClasses(type: Copy) {
    from "build/tmp/kotlin-classes/debug"
    into "build/intermediates/classes/debug"
}

configurations {
    // answer limdale: https://stackoverflow.com/questions/54643053/gradle-error-after-upgrading-to-andoidx-the-latest-android-gradle-plugin
    androidTestImplementation.exclude module: 'guava' // for proper compile unsigned APK. Otherwise, due to the announcement "com.google.truth.extensions:truth-java8-extension:0.42", an error occurs
    //androidTestImplementation.exclude module: 'error_prone_annotations'
    //androidTestImplementation.exclude module: 'checker-qual'
}